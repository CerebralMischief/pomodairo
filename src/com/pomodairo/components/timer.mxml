<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="282" 
	height="80"  
	backgroundAlpha="0.0"
	initialize="init()"
	verticalScrollPolicy="off">
	
	    <mx:Script>
        <![CDATA[
        	import com.pomodairo.events.PomodoroEvent;
        	import com.pomodairo.PomodoroEventDispatcher;
        	import com.pomodairo.Pomodoro;
        	import mx.binding.utils.BindingUtils;
        	import flash.utils.getTimer;
            import flash.utils.Timer;
            import flash.events.TimerEvent;
		    import flash.display.Sprite;
		    import flash.media.Sound;
		    import flash.media.SoundChannel;
		    
		    private static var DEFAULT:String = "default";
		    private static var TIME_OUT:String = "timeout";
		    
        	[Embed(source="alarm.mp3")]
        	public var alarmClass:Class;
        	
        	[Embed(source="ticking.mp3")]
        	public var tickClass:Class;

			[Bindable]
			public var pomodoro:Pomodoro;

            private const MIN_MASK:String = "00";
            private const SEC_MASK:String = "00";
            private const TIMER_INTERVAL:int = 10;
            private const DELAY_BEFORE_BREAK_STARTS:int = 5; // Seconds

			[Bindable]
			public var pomodoroLengthInMinutes:int = 25;

            private var baseTimer:int;
            private var pomodoroTimer:Timer;
            private var breakTimer:Timer;

            private function init():void {
                pomodoroTimer = new Timer(TIMER_INTERVAL);
                pomodoroTimer.addEventListener(TimerEvent.TIMER, updateTimer);
                
				breakTimer = new Timer(TIMER_INTERVAL);
                breakTimer.addEventListener(TimerEvent.TIMER, updateBreakTimer);    

                PomodoroEventDispatcher.getInstance().addEventListener(PomodoroEvent.SELECTED, onPomodoroSelected);   	
            }

			private function onPomodoroSelected(e:PomodoroEvent):void {
				pomodoro = e.pomodoro;
				newTaskButton.visible = false;
				startTimerButton.visible = true;
				setTimerStyle(DEFAULT);
			}
			
			private function setTimerStyle(status:String):void {
				switch (status) {
					case DEFAULT:
						counter.setStyle("color", "Yellow");
						break;
						
					case TIME_OUT:
						counter.setStyle("color", "Red");
						break;	
				}
			}

            private function updateTimer(evt:TimerEvent):void {
            	if (getTimer() >= baseTimer) {
            		var alarm:Sound = new alarmClass() as Sound;
            		alarm.play();
            		PomodoroEventDispatcher.getInstance().dispatchEvent(new PomodoroEvent(PomodoroEvent.TIME_OUT));
            		pomodoroTimer.stop();
            		setTimerStyle(TIME_OUT);
            		startBreakTimer();
            	} else {
	                var d:Date = new Date(baseTimer - getTimer());
	                var min:String = (MIN_MASK + d.minutes).substr(-MIN_MASK.length);
	                var sec:String = (SEC_MASK + d.seconds).substr(-SEC_MASK.length);
	                counter.text = String(min + ":" + sec);
	                if (min == "00") {
	                	setTimerStyle(TIME_OUT);
	                }
	            }
            }
            
            private function startBreakTimer():void
            {
            	baseTimer = getTimer();
            	breakTimer.start();            	
            }
            
            private function updateBreakTimer(evt:TimerEvent):void 
            {
			    var d:Date = new Date(getTimer() - baseTimer);
            	if (d.minutes == 0 && d.seconds > DELAY_BEFORE_BREAK_STARTS)
            	{
            		// Start the break counter after 10 seconds.
            		setTimerStyle(DEFAULT);
            		showBreakButton();
            		pomodoroLabel.text = "On Break";
	                var min:String = (MIN_MASK + d.minutes).substr(-MIN_MASK.length);
	                var sec:String = (SEC_MASK + (d.seconds - DELAY_BEFORE_BREAK_STARTS)).substr(-SEC_MASK.length);
                	counter.text = String(min + ":" + sec);
            	}
            }
            
            private function stopBreakTimer():void
            {
            	breakTimer.stop();
            	pomodoroLabel.text = "";
            	showNewTaskButton();
            }

            private function startTimer(event:Event = null):void {
            	setTimerStyle(DEFAULT);
                baseTimer = getTimer();
                baseTimer += pomodoroLengthInMinutes*60*1000+1000;
                //baseTimer += 6*1000;
                pomodoroTimer.start();
                showStopButton();
                
				var startEvent:PomodoroEvent = new PomodoroEvent(PomodoroEvent.START_POMODORO);
				startEvent.pomodoro = pomodoro;
				PomodoroEventDispatcher.getInstance().dispatchEvent(startEvent);
                
                // Play sound
            	var tick:Sound = new tickClass() as Sound;
            }
            
            private function showBreakButton():void
            {
        		startTimerButton.visible = false;
        		stopTimerButton.visible = false;
        		stopBreakButton.visible = true;
            }            
            
            private function showStartButton():void
            {
        		startTimerButton.visible = true;
        		stopTimerButton.visible = false;
        		stopBreakButton.visible = false;
        		counter.text = pomodoroLengthInMinutes+":00";
        		setTimerStyle(DEFAULT);
            }
            
            private function showStopButton():void
            {
            	startTimerButton.visible = false;
            	stopTimerButton.visible = true;            	
            }
            
            private function showNewTaskButton():void
            {
            	startTimerButton.visible = false;
            	stopTimerButton.visible = false;
            	stopBreakButton.visible = false;
            	newTaskButton.visible = true; 
            	counter.text = pomodoroLengthInMinutes+":00";           	
            }            

            private function stopTimer(event:Event=null):void {
                pomodoroTimer.stop();
                showStartButton();
            }
            
            private function createPomodoro():void {
            	var input:PomoItemInput = new PomoItemInput();
            	input.type = Pomodoro.TYPE_POMODORO;
            	input.x = 10;
            	input.y = 10;
            	input.selectAfterCreate = true;
            	this.addChild(input);
            }
            
            private function toTwoDigitString(number:int):String
            {
            	if (number < 10)
            	{
            		return ""+0+number;
            	}
            	return ""+number;
            }
        ]]>
    </mx:Script>
    
	<mx:Button id="startTimerButton" label="Start Timer" click="startTimer()" x="192" y="17" visible="false"/>
	<mx:Button id="stopTimerButton" label="Stop Timer" click="stopTimer()" x="192" y="17" visible="false"/>
	<mx:Button id="stopBreakButton" label="Stop Break" click="stopBreakTimer()" x="192" y="17" visible="false"/>
	<mx:Button id="newTaskButton" label="New Task" click="createPomodoro()" x="192" y="17"/>
	<mx:Label id="counter" fontSize="60"  text="{toTwoDigitString(pomodoroLengthInMinutes)}:00" color="#FFFF00" x="0" y="-7" height="69"/>
	<mx:Label id="pomodoroLabel" x="10" y="65" text="{pomodoro.name}" width="262" height="17"/>
    
</mx:Canvas>
