<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="312" 
	height="80"  
	backgroundAlpha="0.0"
	initialize="init()"
	verticalScrollPolicy="off" horizontalScrollPolicy="off">
	
	    <mx:Script>
        <![CDATA[
        	import com.pomodairo.events.ConfigurationUpdatedEvent;
        	import com.pomodairo.ConfigProperty;
        	import com.pomodairo.db.Storage;
        	import mx.managers.PopUpManager;
        	import com.pomodairo.events.PomodoroEvent;
        	import com.pomodairo.PomodoroEventDispatcher;
        	import com.pomodairo.Pomodoro;
        	import mx.binding.utils.BindingUtils;
        	import flash.utils.getTimer;
            import flash.utils.Timer;
            import flash.events.TimerEvent;
		    import flash.display.Sprite;
		    import flash.media.Sound;
		    import flash.media.SoundChannel;
		    
		    private static var DEFAULT:String = "default";
		    private static var TIME_OUT:String = "timeout";
		    private static var BREAK:String = "break";
		    
        	[Embed(source="alarm.mp3")]
        	public var alarmClass:Class;
        	
        	[Embed(source="ticking.mp3")]
        	public var tickClass:Class;

			[Bindable]
			public var pomodoro:Pomodoro;

            private const MIN_MASK:String = "00";
            private const SEC_MASK:String = "00";
            private const TIMER_INTERVAL:int = 10;
            private const DELAY_BEFORE_BREAK_STARTS:int = 5000; // Milliseconds

			[Bindable]
			public var pomodoroLengthInMinutes:Number = 25;

            private var baseTimer:int;
            private var pomodoroTimer:Timer;
            private var breakTimer:Timer;
            
            private var blinkTimer:Timer = new Timer(300, 0);
            
            private var soundEnabled:Boolean = true;

            private function init():void {
                pomodoroTimer = new Timer(TIMER_INTERVAL);
                pomodoroTimer.addEventListener(TimerEvent.TIMER, updateTimer);
                
				breakTimer = new Timer(TIMER_INTERVAL);
                breakTimer.addEventListener(TimerEvent.TIMER, updateBreakTimer);    

                PomodoroEventDispatcher.getInstance().addEventListener(PomodoroEvent.SELECTED, onPomodoroSelected);
                PomodoroEventDispatcher.getInstance().addEventListener(ConfigurationUpdatedEvent.UPDATED, onConfigurationChange);   	   	
            }

			private function onPomodoroSelected(e:PomodoroEvent):void {
				pomodoro = e.pomodoro;
				stopBreakTimer();
				showStartButton();
			}
			
			private function onConfigurationChange(e:ConfigurationUpdatedEvent):void {
				if (e.configElement.name == "pomodoroLength") 
				{
					pomodoroLengthInMinutes = new Number(e.configElement.value);
				}
				
				if (e.configElement.name == "sound") 
				{
					soundEnabled = e.configElement.value == "true";
				}
				
				if (e.configElement.name == "volume") 
				{
					// TODO: Set volume here!
					// something = new Number(e.configElement.value);
				}
			}
			
			private function setTimerStyle(status:String):void {
				switch (status) {
					case DEFAULT:
						counter.setStyle("color", "Yellow");
						stopBlink();
						counter.visible = true;
						break;
						
					case TIME_OUT:
						counter.setStyle("color", "Red");
						break;
						
					case BREAK:
						counter.setStyle("color", "Green");
						stopBlink();
						counter.visible = true;
						break;	
				}
			}

            private function updateTimer(evt:TimerEvent):void {
            	if (getTimer() >= baseTimer) {
            		if (soundEnabled) {
	            		var alarm:Sound = new alarmClass() as Sound;
	            		alarm.play();
            		}
            		PomodoroEventDispatcher.getInstance().dispatchEvent(new PomodoroEvent(PomodoroEvent.TIME_OUT));
            		pomodoroTimer.stop();
            		setTimerStyle(TIME_OUT);
            		scheduleBreakTimerStart();
            		startBlink();
            	} else {
	                var d:Date = new Date(baseTimer - getTimer());
	                var min:String = (MIN_MASK + d.minutes).substr(-MIN_MASK.length);
	                var sec:String = (SEC_MASK + d.seconds).substr(-SEC_MASK.length);
	                counter.text = String(min + ":" + sec);
	                if (min == "00") {
	                	setTimerStyle(TIME_OUT);
	                }
	            }
            }
            
            private function startBreakTimer(evt:TimerEvent):void
            {
            	baseTimer = getTimer();
            	breakTimer.start();
            	setTimerStyle(BREAK);
            	showBreakButton();
            	pomodoroLabel.text = "On Break";
            }
            
            private function updateBreakTimer(evt:TimerEvent):void 
            {
			    var d:Date = new Date(getTimer() - baseTimer);
	            var min:String = (MIN_MASK + d.minutes).substr(-MIN_MASK.length);
	            var sec:String = (SEC_MASK + (d.seconds)).substr(-SEC_MASK.length);
                counter.text = String(min + ":" + sec);
            }
            
            private function scheduleBreakTimerStart():void
            {
            	var delayBeforeBreakStartTimer:Timer = new Timer(DELAY_BEFORE_BREAK_STARTS, 1);
            	delayBeforeBreakStartTimer.addEventListener(TimerEvent.TIMER, startBreakTimer);
            	delayBeforeBreakStartTimer.start();
            }
            
            private function stopBreakTimer():void
            {
            	breakTimer.stop();
            	showStartButton();
            }

            private function startTimer(event:Event = null):void {
            	setTimerStyle(DEFAULT);
                baseTimer = getTimer();
                baseTimer += pomodoroLengthInMinutes*60*1000+1000;
                // baseTimer += 6*1000;
                pomodoroTimer.start();
                showStopButton();
                
                // Dispatch event
				var startEvent:PomodoroEvent = new PomodoroEvent(PomodoroEvent.START_POMODORO);
				startEvent.pomodoro = pomodoro;
				PomodoroEventDispatcher.getInstance().dispatchEvent(startEvent);
                
                // Play sound
            	var tick:Sound = new tickClass() as Sound;
            }
            
            private function showBreakButton():void
            {
        		startTimerButton.visible = false;
        		stopTimerButton.visible = false;
        		stopBreakButton.visible = true;
            }            
            
            private function showStartButton():void
            {
        		startTimerButton.visible = true;
        		stopTimerButton.visible = false;
        		stopBreakButton.visible = false;
        		newTaskButton.visible = false;
        		
        		counter.text = pomodoroLengthInMinutes+":00";
        		pomodoroLabel.text = pomodoro.name;
        		setTimerStyle(DEFAULT);
            }
            
            private function showStopButton():void
            {
            	startTimerButton.visible = false;
            	stopTimerButton.visible = true;            	
            }
            
            private function showNewTaskButton():void
            {
            	startTimerButton.visible = false;
            	stopTimerButton.visible = false;
            	stopBreakButton.visible = false;
            	newTaskButton.visible = true; 
            	counter.text = pomodoroLengthInMinutes+":00";           	
            }            

            private function stopTimer(event:Event=null):void {
                pomodoroTimer.stop();
                showStartButton();
            }
            
            private function createPomodoro():void {
            	var input:PomoItemInput = new PomoItemInput();
            	input.type = Pomodoro.TYPE_POMODORO;
            	input.selectAfterCreate = true;
            	PopUpManager.addPopUp(input, this, true);
                PopUpManager.centerPopUp(input);
            }
            
            private function toTwoDigitString(number:int):String
            {
            	if (number < 10)
            	{
            		return ""+0+number;
            	}
            	return ""+number;
            }
            
            private function showSettingsDialog():void {
            	var dialog:ConfigPanel = new ConfigPanel();
            	PopUpManager.addPopUp(dialog, this, true);
                PopUpManager.centerPopUp(dialog);
            }
            
            
            public function startBlink():void
	        {
	            blinkTimer.addEventListener(TimerEvent.TIMER, toggleText );
	            blinkTimer.start();
	        }
	        
	        public function stopBlink():void
	        {
	            blinkTimer.stop();
	        }
	
	        public function toggleText( event:TimerEvent ):void
	        {
	        	counter.visible = !counter.visible;
	        }

        ]]>
    </mx:Script>
    
	<mx:Button id="startTimerButton" label="Start Timer" click="startTimer()"   x="192" y="10" height="40" width="90" visible="false"/>
	<mx:Button id="stopTimerButton" label="Stop Timer" click="stopTimer()"      x="192" y="10" height="40" width="90" visible="false"/>
	<mx:Button id="stopBreakButton" label="Stop Break" click="stopBreakTimer()" x="192" y="10" height="40" width="90" visible="false"/>
	<mx:Button id="newTaskButton" label="New Task" click="createPomodoro()"     x="192" y="10" height="40" width="90" />
	
	<mx:Label id="counter" fontSize="60"  text="{toTwoDigitString(pomodoroLengthInMinutes)}:00" color="#FFFF00" x="0" y="-7" height="69"/>
	<mx:Label id="pomodoroLabel" x="10" y="65" text="{pomodoro.name}" width="90%" height="17"/>
    <mx:Image id="settingsButton" x="292" y="10" source="assets/appicons/settings.png" mouseDown="showSettingsDialog()" toolTip="Edit settings" 
    	useHandCursor="true" buttonMode="true" mouseChildren="false" />
</mx:Canvas>
