<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="320" height="300"
	initialize="init()"	horizontalScrollPolicy="off" verticalScrollPolicy="off">
	
	<mx:Script>
		<![CDATA[
			import com.pomodairo.events.PomodoroEvent;
			import com.pomodairo.components.CustomRenderer;
			import com.pomodairo.PomodoroEventDispatcher;
			import com.pomodairo.Pomodoro;
			import com.pomodairo.TableUtils;
			import com.pomodairo.db.Storage;
			
			//import org.osflash.thunderbolt.Logger;
			
			private var db:Storage = Storage.instance;
			
			private function init():void {
				db.initAndOpenDatabase();
				nameInput.addEventListener(KeyboardEvent.KEY_DOWN,checkSubmit);
				estimatedInput.addEventListener(KeyboardEvent.KEY_DOWN,checkSubmit);
			}
			
			private function onUsersDataGridChanged(event:Event):void{}
	        
	        private function onRemovePomodoroClicked(event:MouseEvent):void
	        {
	        	db.remove(pomodorosDataGrid.selectedItem as Pomodoro);
	        }
	        
	        function checkSubmit(event:KeyboardEvent):void
            {
                if(event.charCode == 13) {
                    onAddPomodoroClicked();
                	nameInput.setFocus();
                }
            }
            
			private function onAddPomodoroClicked(event:Event=null):void
	        {
	        	var pom:Pomodoro = new Pomodoro();
	        	pom.name = nameInput.text;
	        	if(estimatedInput.text != null && estimatedInput.text != "") {
	        		pom.estimated = int(estimatedInput.text);
	        	}
	        	pom.type = Pomodoro.TYPE_POMODORO;
	        	db.addPomodoro(pom);
	        	nameInput.text = "";
	        	estimatedInput.text = "";
	        } 
	        
	        private function onStartPomodoroClicked(event:Event=null):void
	        {
	        	var pom:Pomodoro = pomodorosDataGrid.selectedItem as Pomodoro;
	        	var evt:PomodoroEvent = new PomodoroEvent(PomodoroEvent.SELECTED);
	        	evt.pomodoro = pom;
	        	PomodoroEventDispatcher.getInstance().dispatchEvent(evt);
	        } 
	        
	        private function onDonePomodoroClicked(event:Event=null):void
	        {
	        	if (!pomodorosDataGrid.selectedItem) return;
	        		
	        	var pom:Pomodoro = pomodorosDataGrid.selectedItem as Pomodoro;
	        	if(pom.done) {
	        		pom.closed = null;
	        	}
	        	else {
	        		pom.closed = new Date();
	        	}
	        	pom.done = !pom.done;
	        	// Following is done by event listener in db object.
	        	// db.markDone(pom);
	        	// Dispatch event.
	        	var evt:PomodoroEvent = new PomodoroEvent(PomodoroEvent.DONE);
	        	evt.pomodoro = pom;
	        	PomodoroEventDispatcher.getInstance().dispatchEvent(evt);
	        }	  
	        
	        private function onClosePomodoroClicked(event:Event=null):void
	        {
				for each (var pom:Pomodoro in db.dataset) {
					if (pom.done) {
			        	pom.visible = false;
			        	db.updateVisibility(pom);
		   			}
				}
	        }	
	        
	       public function copyTableToClipboard(title:String, data:Array):void
           {
			  var tmpString:String = new TableUtils().getPomodorosTableAsCsv(data);
              System.setClipboard(tmpString);
           }
			
		]]>
	</mx:Script>
	
	<mx:DataGrid id="pomodorosDataGrid"
		dataProvider="{db.dataset}" 
		height="241"
		editable="false"
		itemClick="onStartPomodoroClicked(event)"
		change="onUsersDataGridChanged(event)" width="100%" >
		<mx:columns>
			<mx:DataGridColumn width="125" headerText="Name" dataField="name" itemRenderer="com.pomodairo.components.CustomRenderer" />
			<mx:DataGridColumn width="28" headerText="E" dataField="estimated" dataTipField="estimatedDescription" showDataTips="true" itemRenderer="com.pomodairo.components.CustomRenderer" />
			<mx:DataGridColumn width="28" headerText="P" dataField="pomodoros" dataTipField="pomodorosDescription" showDataTips="true" itemRenderer="com.pomodairo.components.CustomRenderer" />
			<mx:DataGridColumn width="28" headerText="U" dataField="unplanned" dataTipField="unplannedDescription" showDataTips="true" itemRenderer="com.pomodairo.components.CustomRenderer" />
			<mx:DataGridColumn width="28" headerText="I" dataField="interruptions" dataTipField="interruptionsDescription" showDataTips="true" itemRenderer="com.pomodairo.components.CustomRenderer" />
		</mx:columns>
	</mx:DataGrid>
	
	<mx:ControlBar y="265" height="35" width="320">
		<mx:Button id="removePomoButton" 
			label="Remove" enabled="{pomodorosDataGrid.selectedItem}"
			click="onRemovePomodoroClicked(event)" width="83" height="24" toolTip="Remove this pomodoro from the list"/>
		<mx:Button label="Clean" id="clearButton" click="onClosePomodoroClicked(event)" toolTip="Removes all 'done' items"/>
		<mx:Button label="Done" id="closePomoButton" width="70" height="24" enabled="{pomodorosDataGrid.selectedItem}" click="onDonePomodoroClicked()" toolTip="Mark as done"/>
		<mx:Button label="Copy" id="copyToClipboard" width="65" height="24" click="copyTableToClipboard('Task list', db.dataset)" toolTip="Copy table to clipboard"/>
	</mx:ControlBar>
	<mx:TextInput id="nameInput" color="#FFFFFF" width="170" x="10" y="242" backgroundColor="#FFF8F8"/>
	<mx:TextInput id="estimatedInput" color="#FFFFFF" width="30" x="180" y="242" backgroundColor="#FFF8F8" maxChars="1" restrict="0-7"/>
	<mx:Button id="addPomoButton" label="Add New" click="onAddPomodoroClicked()" width="91" height="24" x="219" y="241"/>
	

</mx:Canvas>
